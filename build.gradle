plugins {
    id "org.jetbrains.kotlin.jvm" version '1.2.21'
    id 'com.matthewprenger.cursegradle' version '1.0.10'
    id 'idea'
}

subprojects {
    apply plugin: "kotlin"
    apply plugin: "idea"

    if (project.hasProperty('build_number')) {
        mod_version += "-build-$build_number"
    } else if (!project.hasProperty('release')) {
        mod_version += "-dev"
    }

    idea {
        module {
            excludeDirs += [file("run")]
        }
    }

    if (name != "core") {

        repositories {
            jcenter()
            maven {
                name = "unascribed"
                url = 'http://unascribed.com/maven/releases'
            }
            maven {
                name = "shadowfacts"
                url = "http://maven.shadowfacts.net/"
            }
        }

        version = mc_version + "-" + mod_version

        apply plugin: 'com.matthewprenger.cursegradle'
        curseforge {
            if (project.hasProperty('CURSEFORGE_API_TOKEN') && project.hasProperty('release')) {
                apiKey = CURSEFORGE_API_TOKEN
            }
            project {
                id = '287323'
                releaseType = 'beta'
                if (project.hasProperty('changelog_file')) {
                    println("changelog = $changelog_file")
                    changelogType = "markdown"
                    changelog = file(changelog_file)
                }
                relations {
                    requiredLibrary 'shadowfacts-forgelin'
                }
                mainArtifact(jar) {
                    displayName = "MatterLink $version"
                }
            }
        }


    }
}